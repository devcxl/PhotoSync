plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.compose)
    // kapt plugin is applied below using the legacy apply plugin to avoid plugin classpath/version conflicts
}

apply plugin: 'kotlin-kapt'

android {
    namespace = "cn.devcxl.photosync"
    compileSdk = 36

    defaultConfig {
        applicationId "cn.devcxl.photosync"
        minSdk = 24
        targetSdk = 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"


        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments "-DANDROID_STL=c++_shared"
            }
        }

        // Only build ABI(s) we have prebuilt libraw for. Add more when you add their libraw.so.
        ndk {
            abiFilters "arm64-v8a"
        }
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }

    // 设置默认不开启签名 (避免 debug 模式下签名报错)
    signingConfigs {
        release {
            // Resolve values from environment variables first, then project properties.
            def keystorePath = System.getenv("KEYSTORE_PATH") ?: project.findProperty("KEYSTORE_PATH")
            if (keystorePath) {
                storeFile file(keystorePath)
            }

            def keystorePassword = System.getenv("KEYSTORE_PASSWORD") ?: project.findProperty("KEYSTORE_PASSWORD")
            if (keystorePassword) {
                storePassword keystorePassword
            }

            def keyAliasValue = System.getenv("KEY_ALIAS") ?: project.findProperty("KEY_ALIAS")
            if (keyAliasValue) {
                keyAlias keyAliasValue
            }

            def keyPasswordValue = System.getenv("KEY_PASSWORD") ?: project.findProperty("KEY_PASSWORD")
            if (keyPasswordValue) {
                keyPassword keyPasswordValue
            }
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.release
            minifyEnabled = true
            shrinkResources = true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding = true
    }
}
dependencies {

    implementation libs.androidx.core.ktx
    implementation libs.androidx.lifecycle.runtime.ktx
    implementation libs.androidx.constraintlayout

    // Required by PhotoView
    implementation libs.androidx.appcompat

    implementation libs.androidx.activity.compose
    implementation platform(libs.androidx.compose.bom)
    implementation libs.androidx.ui
    implementation libs.androidx.ui.graphics
    implementation libs.androidx.ui.tooling.preview
    implementation libs.androidx.material3
    // Room
    implementation libs.androidx.room.runtime
    implementation libs.androidx.room.ktx
    kapt libs.androidx.room.compiler
    implementation libs.timber

     implementation libs.opencv

    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    androidTestImplementation platform(libs.androidx.compose.bom)
    androidTestImplementation libs.androidx.ui.test.junit4
    debugImplementation libs.androidx.ui.tooling
    debugImplementation libs.androidx.ui.test.manifest

    // Added for gallery: swipe and pinch-to-zoom
    implementation libs.androidx.viewpager2
    implementation libs.photoview

}